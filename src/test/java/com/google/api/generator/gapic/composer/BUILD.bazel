load("//:rules_bazel/java/java_diff_test.bzl", "updated_golden")

package(default_visibility = ["//visibility:public"])

TESTS = [
    "BatchingDescriptorComposerTest",
    "ComposerTest",
    "DefaultValueComposerTest",
    "GrpcServiceCallableFactoryClassComposerTest",
    "GrpcServiceStubClassComposerTest",
    "MockServiceClassComposerTest",
    "MockServiceImplClassComposerTest",
    "ResourceNameHelperClassComposerTest",
    "ResourceNameTokenizerTest",
    "RetrySettingsComposerTest",
    "ServiceClientClassComposerTest",
    "ServiceClientTestClassComposerTest",
    "ServiceSettingsClassComposerTest",
    "ServiceStubSettingsClassComposerTest",
    "ServiceStubClassComposerTest",
]

UPDATE_GOLDENS_TESTS = [
    "BatchingDescriptorComposerTest",
    "ComposerTest",
    "GrpcServiceCallableFactoryClassComposerTest",
    "GrpcServiceStubClassComposerTest",
    "MockServiceClassComposerTest",
    "MockServiceImplClassComposerTest",
    "ResourceNameHelperClassComposerTest",
    "ServiceSettingsClassComposerTest",
    "ServiceStubSettingsClassComposerTest",
    "ServiceStubClassComposerTest",
]

filegroup(
    name = "composer_files",
    srcs = glob(["*.java"]),
)

java_proto_library(
    name = "pubsub_java_proto",
    deps = [
        "@com_google_googleapis//google/pubsub/v1:pubsub_proto",
    ],
)

java_proto_library(
    name = "common_resources_java_proto",
    deps = [
        "@com_google_googleapis//google/cloud:common_resources_proto",
    ],
)

[java_test(
    name = test_name,
    srcs = [
        "{0}.java".format(test_name), 
        "ComposerConstants.java",
    ],
    data = [
        "//src/test/java/com/google/api/generator/gapic/composer/goldens:goldens_files",
        "//src/test/java/com/google/api/generator/gapic/testdata:gapic_config_files",
        "//src/test/java/com/google/api/generator/gapic/testdata:service_config_files",
    ],
    test_class = "com.google.api.generator.gapic.composer.{0}".format(test_name),
    deps = [
        ":common_resources_java_proto",
        "//:service_config_java_proto",
        "//src/main/java/com/google/api/generator/engine/ast",
        "//src/main/java/com/google/api/generator/engine/writer",
        "//src/main/java/com/google/api/generator/gapic/composer",
        "//src/test/java/com/google/api/generator/test/framework",
        "//src/main/java/com/google/api/generator/gapic/model",
        "//src/main/java/com/google/api/generator/gapic/protoparser",
        "//src/test/java/com/google/api/generator/gapic/testdata:showcase_java_proto",
        "//src/test/java/com/google/api/generator/gapic/testdata:testgapic_java_proto",
        "@com_google_api_gax_java//gax",
        "@com_google_googleapis//google/logging/v2:logging_java_proto",
        "@com_google_googleapis//google/pubsub/v1:pubsub_java_proto",
        "@com_google_googleapis//google/rpc:rpc_java_proto",
        "@com_google_protobuf//:protobuf_java",
        "@com_google_truth_truth//jar",
        "@junit_junit//jar",
    ],
) for test_name in TESTS]



TEST_CLASS_DIR = "com.google.api.generator.gapic.composer."
# Run `bazel run src/test/java/com/google/api/generator/gapic/composer:testTargetName_update`
# to update goldens as expected generated code.
# `ServiceClient*` tests are not supported now since they are still in active development.

[updated_golden(
    name = test_name + "_update",
    srcs = [
        ":composer_files",
        "//src/test/java/com/google/api/generator/gapic/testdata:gapic_config_files",
        "//src/test/java/com/google/api/generator/gapic/testdata:service_config_files",
        "//src/test/java/com/google/api/generator/gapic/composer/goldens:goldens_files",
        "//src/test/java/com/google/api/generator/test/framework:framework_files",
    ],
    test_class_name = TEST_CLASS_DIR + test_name,
) for test_name in UPDATE_GOLDENS_TESTS] 
