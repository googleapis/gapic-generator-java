load(
    "@com_google_googleapis_imports//:imports.bzl",
    java_gapic_assembly_gradle_pkg = "java_gapic_assembly_gradle_pkg2",
    java_gapic_library = "java_gapic_library2",
    java_gapic_test = "java_gapic_test2",
)
load(
    "//:rules_bazel/java/integration_test.bzl",
    "golden_update",
    "integration_test",
)

package(default_visibility = ["//visibility:public"])

####################################################
# Integration Test Rules
####################################################

INTEGRATION_TEST_LIBRARIES = [
    "asset",
    "logging",
    "redis",
]

[integration_test(
    name = lib_name,
    data = ["//test/integration/goldens/%s:goldens_files" % lib_name],
    target = ":%s_java_gapic" % lib_name,
) for lib_name in INTEGRATION_TEST_LIBRARIES]

[golden_update(
    name = "%s_update" % lib_name,
    data = ["//test/integration/goldens/%s:goldens_files" % lib_name],
    target = ":%s_java_gapic" % lib_name,
) for lib_name in INTEGRATION_TEST_LIBRARIES]

####################################################
# API Library Rules
####################################################
# These will eventually go away once more APIs in googleapis have been migrated to the
# microgenerator.

# Asset API.
java_gapic_library(
    name = "asset_java_gapic",
    srcs = ["@com_google_googleapis//google/cloud/asset/v1:asset_proto_with_info"],
    grpc_service_config = "@com_google_googleapis//google/cloud/asset/v1:cloudasset_grpc_service_config.json",
    package = "google.cloud.asset.v1",
    test_deps = [
        "@com_google_googleapis//google/cloud/asset/v1:asset_java_grpc",
        "@com_google_googleapis//google/iam/v1:iam_java_grpc",
    ],
    deps = [
        "@com_google_googleapis//google/cloud/asset/v1:asset_java_proto",
        "@com_google_googleapis//google/iam/v1:iam_java_proto",
        "@com_google_googleapis//google/identity/accesscontextmanager/v1:accesscontextmanager_proto",
        "@com_google_googleapis//google/type:type_java_proto",
    ],
)

java_gapic_test(
    name = "asset_java_gapic_test_suite",
    test_classes = [
        "com.google.cloud.asset.v1.AssetServiceClientTest",
    ],
    runtime_deps = [":asset_java_gapic_test"],
)

java_gapic_assembly_gradle_pkg(
    name = "google-cloud-asset-v1-java",
    deps = [
        ":asset_java_gapic",
        "@com_google_googleapis//google/cloud/asset/v1:asset_java_grpc",
        "@com_google_googleapis//google/cloud/asset/v1:asset_java_proto",
        "@com_google_googleapis//google/cloud/asset/v1:asset_proto",
        "@com_google_googleapis//google/cloud/orgpolicy/v1:orgpolicy_java_proto",
        "@com_google_googleapis//google/identity/accesscontextmanager/type:type_java_proto",
        "@com_google_googleapis//google/identity/accesscontextmanager/v1:accesscontextmanager_java_proto",
    ],
)

# Redis API.
java_gapic_library(
    name = "redis_java_gapic",
    srcs = ["@com_google_googleapis//google/cloud/redis/v1:redis_proto_with_info"],
    gapic_yaml = "@com_google_googleapis//google/cloud/redis/v1:redis_gapic.yaml",
    grpc_service_config = "@com_google_googleapis//google/cloud/redis/v1:redis_grpc_service_config.json",
    package = "google.cloud.redis.v1",
    test_deps = [
        "@com_google_googleapis//google/cloud/redis/v1:redis_java_grpc",
    ],
    deps = [
        "@com_google_googleapis//google/cloud/redis/v1:redis_java_proto",
    ],
)

java_gapic_test(
    name = "redis_java_gapic_test_suite",
    test_classes = [
        "com.google.cloud.redis.v1.CloudRedisClientTest",
    ],
    runtime_deps = [":redis_java_gapic_test"],
)

java_gapic_assembly_gradle_pkg(
    name = "google-cloud-redis-v1-java",
    deps = [
        ":redis_java_gapic",
        "@com_google_googleapis//google/cloud/redis/v1:redis_java_grpc",
        "@com_google_googleapis//google/cloud/redis/v1:redis_java_proto",
        "@com_google_googleapis//google/cloud/redis/v1:redis_proto",
    ],
)

# Logging API
java_gapic_library(
    name = "logging_java_gapic",
    srcs = ["@com_google_googleapis//google/logging/v2:logging_proto_with_info"],
    gapic_yaml = "@com_google_googleapis//google/logging/v2:logging_gapic.yaml",
    grpc_service_config = "@com_google_googleapis//google/logging/v2:logging_grpc_service_config.json",
    package = "google.logging.v2",
    test_deps = [
        "@com_google_googleapis//google/logging/v2:logging_java_grpc",
    ],
    deps = [
        "@com_google_googleapis//google/api:api_java_proto",
        "@com_google_googleapis//google/logging/v2:logging_java_proto",
    ],
)

java_gapic_test(
    name = "logging_java_gapic_test_suite",
    test_classes = [
        "com.google.logging.v2.ConfigServiceV2ClientTest",
        "com.google.logging.v2.LoggingServiceV2ClientTest",
        "com.google.logging.v2.MetricsServiceV2ClientTest",
    ],
    runtime_deps = [":logging_java_gapic_test"],
)

java_gapic_assembly_gradle_pkg(
    name = "google-cloud-logging-v2-java",
    deps = [
        ":logging_java_gapic",
        "@com_google_googleapis//google/logging/v2:logging_java_grpc",
        "@com_google_googleapis//google/logging/v2:logging_java_proto",
        "@com_google_googleapis//google/logging/v2:logging_proto",
    ],
)
